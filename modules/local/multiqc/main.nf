#!/usr/bin/env nextflow

process MULTIQC {
    label 'process_low'

    conda "${moduleDir}/environment.yml"
    container "${workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container
        ? 'https://depot.galaxyproject.org/singularity/multiqc:1.26--pyhdfd78af_0'
        : 'biocontainers/multiqc:1.33--pyhdfd78af_0'}"

    input:
    path multiqc_files
    path multiqc_config

    output:
    path "*.html", emit: report
    path "*_data", emit: data
    path "*_plots", optional: true, emit: plots
    path "versions.yml", emit: versions

    when:
    task.ext.when == null || task.ext.when

    script:
    def args = task.ext.args ?: ''
    def config = multiqc_config ? "--config ${multiqc_config}" : ''
    """
    multiqc \\
        --force \\
        ${args} \\
        ${config} \\
        ${multiqc_files} \\

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        multiqc: \$( multiqc --version | sed 's/.*version //' )
    END_VERSIONS
    """

    stub:
    """
    mkdir multiqc_data
    mkdir multiqc_plots
    touch multiqc_report.html

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        multiqc: \$( multiqc --version | sed 's/.*version //' )
    END_VERSIONS
    """
}
